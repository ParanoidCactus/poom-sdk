actor BlueArmor : Armor 2018
{
  Radius 20
  Height 16
  Amount 200
  PickupSound 61
  States
  {
  Spawn:
    ARM1 A 6 Bright
	ARM1 A 6
    Loop
  }
}

actor RedArmor : Armor 2019
{
  Radius 20
  Height 16
  Amount 50
  PickupSound 61
  States
  {
  Spawn:
    ARM3 A 6 Bright
	ARM3 A 6
    Loop
  }
}

actor GoldArmor : Armor 2020
{
  Radius 20
  Height 16
  Amount 100
  PickupSound 61
  States
  {
  Spawn:
    ARM2 A 6 Bright
	ARM2 A 6
    Loop
  }
}

actor HealthBonus : Health 2014
{
  Radius 20
  Height 16
  Amount 1
  MaxAmount 200
  PickupSound 61
  States
  {
  Spawn:
    BON1 A 12 Bright
	BON1 B 6 Bright
	BON1 C 8 Bright
	BON1 B 6 Bright
    Loop
  }
}

actor MedKit : Health 2016
{
  Radius 20
  Height 16
  Amount 15
  MaxAmount 100
  PickupSound 61
  States
  {
  Spawn:
    STIM A 6 Bright
	STIM A 6
    Loop
  }
}

actor ArmorBonus : Armor 2015
{
  Radius 20
  Height 16
  Amount 1
  States
  {
  Spawn:
    BON2 A 6 Bright
	BON2 B 6 Bright
	BON2 C 6 Bright
	BON2 D 6 Bright
	BON2 C 6 Bright
	BON2 B 6 Bright
    Loop
  }
}


actor RedCard:Key 13
{
 Radius 20
 Height 16
 SlotNumber 1
 Amount 1
 MaxAmount 1
 PickupSound 61
 HUDColor 13
 Icon 17
 States
 {
  Spawn:
   RKEY A 6 Bright
   RKEY A 6
   Loop
 }
}

actor BlueCard:Key 5
{
 Radius 20
 Height 16
 SlotNumber 2
 Amount 1
 MaxAmount 1
 PickupSound 61
 HUDColor 15
 Icon 17
 +SHOOTABLE
 States
 {
  Spawn:
   BKEY A 6 Bright
   BKEY A 6
   Loop
 }
}

actor YellowCard:Key 6
{
 Radius 20
 Height 16
 SlotNumber 3
 Amount 1
 MaxAmount 1
 PickupSound 61
 HUDColor 11
 Icon 17
 States
 {
  Spawn:
   GKEY A 6 Bright
   GKEY A 6
   Loop
 }
}

actor Column 2027
{
 Radius 4
 Height 20
 +SOLID
 States
 {
	Spawn:
	 COLU A -1
	 Stop
 }
}

actor Torch 2028
{
 Radius 4
 Height 16
 +SOLID
 States
 {
	Spawn:
	 SMRT A 3 Bright
	 SMRT B 3 Bright
	 SMRT C 3 Bright
	 Loop
 }
}

actor Candelabra 35
{
 Radius 4
 Height 20
 +SOLID
 States
 {
	Spawn:
	 CBRA A -1 Bright
	 Stop
 }
}

actor DeadMarine 15
{
  States
  {
  Spawn:
    SPOS K -1
    Stop
  }
}

actor DeadImp 14
{
  States
  {
  Spawn:
    TROO K -1
    Stop
  }
}

actor ExplosiveBarrel 2035
{
  Health 20
  Radius 20
  Height 42
  +SOLID
  +SHOOTABLE
  +NOBLOOD
  +DONTGIB

  States
  {
  Spawn:
    BAR1 A -1
    Stop
  Death:
    BAR1 A 1 Bright A_PlaySound(60)
    EXPB B 6 Bright A_Explode(100, 128);
    EXPB E 6 Bright
    EXPB F 8 Bright
    Stop
  }
}

actor RocketTrail 9993
{
  +NOGRAVITY
  +RANDOMIZE
  States
  {
  Spawn:
    PUFF B 1
    PUFF C 1
    // Intentional fall-through
  Melee:
    PUFF D 1
	PUFF E 1
    Stop
  }
}

actor BulletPuff 9999
{
  +NOGRAVITY
  +RANDOMIZE
  States
  {
  Spawn:
    PUFF A 3 Bright
    PUFF B 3
    // Intentional fall-through
  Melee:
    PUFF C 3
    PUFF D 3
	PUFF E 3
    Stop
  Pain:
	BLOD A 4
    BLOD B 4
	BLOD C 4
	Stop
  }
}

actor BloodPuff 9996
{
  +NOGRAVITY
  +RANDOMIZE
  States
  {
  Spawn:
    BLOD A 8 Bright
    BLOD B 4 Bright
    // Intentional fall-through
  Melee:
    BLOD C 4 Bright
	BLOD C 2
    Stop
  }
}

actor Cell : Ammo 2047
{
	Amount 20
	MaxAmount 300
	PickupSound 54
	// zap pico8 character
	Icon 150
	  States
	  {
	  Spawn:
		CELP A 6 Bright
		CELP A 6
		Loop
	  }
}

actor RocketAmmo : Ammo 2010
{
	Amount 1
	MaxAmount 50
	PickupSound 54
	// 
	Icon 26
	  States
  {
  Spawn:
    ROCK A 6 Bright
	ROCK A 6
    Loop
  }
}

actor RocketBox : RocketAmmo 2046
{
  Amount 5
  PickupSound 54
  States
  {
  Spawn:
    BROK A 6 Bright
	BROK A 6
    Loop
  }
}

actor Clip : Ammo 2007
{
	Amount 20
	MaxAmount 200
	Icon 152
	PickupSound 54
  States
  {
  Spawn:
    AMMO A 6 Bright
	AMMO A 6
    Loop
  }
}

actor Shell : Ammo 2008
{
	Amount 4
	MaxAmount 50
	Icon 153
	PickupSound 54
	  States
	  {
	  Spawn:
		SHEL A 6 Bright
		SHEL A 6
		Loop
	  }
}

actor Fist : Weapon 2000
{
	SlotNumber 5
	HudLabel "fIST"
	States {
		Spawn:
			PUNG A 5
			Loop
		Fire:
			PUNG B 2 A_PlaySound(52);
			PUNG C 5 A_MeleeAttack(15,"BulletPuff");
			PUNG B 4
			Goto Ready
		Ready:
			PUNG A 1 A_WeaponReady;
			Loop
	}
}

actor PlasmaBall : Projectile 9998
{
	Radius 13
	Height 8
	Speed 100
 	Damage 2
 +MISSILE
 States
	{
 	Spawn:
		PLSB A 3 Bright
		PLSB B 3 Bright
		Loop
	Death:
		PLSE A 3 Bright
		PLSE B 3 Bright
		PLSE C 3 Bright
		PLSE D 3 Bright
		Stop
	}
}


actor PlasmaRifle : Weapon 2004
{
	AmmoUse 1
	AmmoGive 80
	AmmoType "Cell"
	SlotNumber 4
	AttackSound 58
	HudLabel "pLASMA gUN"
	PickupSound 54
	States {
		Spawn:
			PLAS A 5
			PLAS A 5 Bright
			Loop
		Fire:
			PLSG A 1 Bright A_Light(160)
			PLSG A 2 Bright A_FireProjectile("PlasmaBall");
			PLSG A 4
			Goto Ready
		Ready:
			PLSG A 1 A_WeaponReady;
			Loop
	}
}

actor ShotGun : Weapon 2001
{
	AmmoUse 1
	AmmoGive 8
	AmmoType "Shell"
	SlotNumber 1
	AttackSound 56
	HudLabel "sHOTGUN"
	PickupSound 54
	States {
		Spawn:
			SHOT A 5
			SHOT A 5 Bright
			Loop
		Fire:
			SHTG B 1 Bright A_Light(160)
			SHTG B 5 Bright A_FireBullets(7,0,3,7,"BulletPuff");
			SHTG A 4
			SHTG D 15
			SHTG A 7
			Goto Ready
		Ready:
			SHTG A 1 A_WeaponReady;
			Loop
	}
}

actor Rocket : Projectile 9997
{
  Radius 11
  Height 8
  Speed 50
  Damage 10
  TrailType "RocketTrail"
  +MISSILE
  States
  {
  Spawn:
    MISL A 120 Bright
	// blast on TTL
  Death:
    EXPL A 1 Bright A_PlaySound(60);
    EXPL A 3 Bright A_Explode(50, 96);
    EXPL B 4 Bright
	EXPL E 4 Bright
    EXPL F 4 Bright
    Stop
  }
}

actor RocketLauncher : Weapon 2003
{
	AmmoUse 1
	AmmoGive 10
	AmmoType "RocketAmmo"
	HudLabel "rOCKET"
	SlotNumber 3
	AttackSound 57
	PickupSound 54
	States {
		Spawn:
			LAUN A 5
			LAUN A 5 Bright
			Loop
		Fire:
			MISG A 8 A_FireProjectile("Rocket");
			MISG B 12
			Goto Ready
		Ready:
			MISG A 1 A_WeaponReady;
			Loop
	}
}

actor ChainGun : Weapon 2002
{
	AmmoUse 1
	AmmoGive 50
	AmmoType "Clip"
	HudLabel "cHAINGUN"
	SlotNumber 2
	AttackSound 55
	PickupSound 54
	States {
		Spawn:
			MGUN A 5
			MGUN A 5 Bright
			Loop
		Fire:
			CHGG C 1 A_FireBullets(2,0,1,6,"BulletPuff");
			CHGG C 1 Bright A_Light(160)
			CHGG B 1 Bright
			CHGG B 1
			Goto Ready
		Ready:
			CHGG A 1 A_WeaponReady;
			Loop
	}
}

actor PoomGuy: PlayerPawn 1
{
	Radius 16
	Health 100
	Armor 50
	Speed 4
	Height 56
	StartItem "ShotGun"
	StartItem "Shell", 50
	StartItem "Fist"
}

actor ZombieMan : Monster 3004
{
  Health 20
  Radius 20
  Height 56
  Speed 8
  PainChance 200
  // DropItem "Clip"
  States
  {
  Spawn:
    SPOS A 10 A_Look;
    Loop
  See:
    SPOS A 8
	SPOS B 8 A_Chase;
    Loop
  Missile:
    SPOS E 9 A_FaceTarget;
	SPOS E 1 A_PlaySound(55);
    SPOS F 8 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff"); //22.5
    SPOS E 8
    Goto See
  Death:
    SPOS H 5
    SPOS I 5 // A_Scream
    SPOS J 5 //A_NoBlocking
    SPOS K -1
    Stop
  }
}

actor DoomImpBall: Projectile 9995
{
  Radius 6
  Height 8
  Speed 10
  FastSpeed 15
  Damage 3
  Mass 33
  +RANDOMIZE
  +MISSILE
  +NOGRAVITY
  States
  {
  Spawn:
    BAL1 A 120 Bright
    Stop
  Death:
	EXPL A 4 Bright A_PlaySound(59);
	EXPL E 4 Bright
    EXPL F 4 Bright
    Stop
  }
}

actor DoomImp: Monster 3001
{
	Health 40
	Radius 32
	Height 56
	Speed 8
	PainChance 200
	MeleeRange 64
	States
	{
		Spawn:
			TROO A 10 A_Look;
			Loop
		See:
			TROO A 5
			TROO B 4 
			TROO B 1 A_Chase;
			Loop
		Melee:
			TROO B 8 A_FaceTarget;
			TROO E 1 Bright A_PlaySound(51);
			TROO E 6 Bright A_MeleeAttack(5, "BloodPuff");
			TROO E 6
			TROO B 4
			Goto See
		Missile:
			TROO B 8 A_FaceTarget;
			TROO B 1 A_PlaySound(53);
			TROO E 12 A_FireProjectile("DoomImpBall");
			TROO B 4
			Goto See
	  Death:
		TROO H 5
		TROO I 5
		TROO J 5
		TROO K -1
		Stop
	}
}

actor CacodemonBall: Projectile 9994
{
  Radius 6
  Height 8
  Speed 25
  FastSpeed 20
  Damage 5
  Mass 33
  +RANDOMIZE
  +MISSILE
  +NOGRAVITY
  States
  {
  Spawn:
    BAL2 A 3 Bright
	BAL2 B 3 Bright
    Loop
  Death:
    EXPL A 4 Bright A_PlaySound(59);
	EXPL E 4 Bright
    EXPL F 4 Bright
    Stop
  }
}

actor Cacodemon: Monster 3002
{
	Health 150
	Radius 32
	Height 56
	Speed 48
	PainChance 200
	MeleeRange 64
	Mass 500
	+FLOAT
	+NOGRAVITY
	+DROPOFF
	States
	{
		Spawn:
			HEAD A 10 A_Look;
			Loop
		See:
			HEAD A 9
			HEAD A 1 A_Chase;
			Loop
		Melee:
			HEAD A 8 A_FaceTarget;
			HEAD B 8
			HEAD B 1 A_MeleeAttack(10, "BloodPuff");
			HEAD A 12 A_PlaySound(51);
			Goto See
		Missile:
			HEAD A 8 A_FaceTarget;
			HEAD E 2 A_PlaySound(53);
			HEAD F 6 Bright A_FireProjectile("CacodemonBall");
			HEAD B 4
			HEAD A 8
			Goto See
	  Death:
		HEAD H 5
		HEAD I 5
		HEAD J 5
		HEAD K 10
		HEAD K -1
		Stop
	}
}

actor LostSoul: Monster 3006
{
	Health 60
	Radius 16
	Height 56
	Speed 16
	PainChance 200
	MeleeRange 256
	Damage 0
	Mass 75
	+FLOAT
	+NOGRAVITY
	+DROPOFF
	+DONTFALL
	States
	{
		Spawn:
			SKUL A 4 Bright 
			SKUL B 4 Bright A_Look;
			Loop
		See:
			SKUL A 4 Bright 
			SKUL B 4 Bright A_Chase;
			Loop
		Melee:
			SKUL A 4 Bright A_FaceTarget;
			SKUL E 4 Bright A_SkullAttack;
			SKUL F 4 Bright A_MeleeAttack(15, "BloodPuff");
			SKUL E 4 Bright A_SkullAttack;
			SKUL F 4 Bright 
			SKUL A 1 Bright A_MeleeAttack(15, "BloodPuff");
			SKUL A 3 Bright A_PlaySound(51);
			SKUL B 4 Bright
			Goto See
		Missile:
			SKUL A 4 Bright A_FaceTarget;
			SKUL E 1 Bright A_SkullAttack;
			SKUL E 3 Bright A_PlaySound(50);
			SKUL F 4 Bright  
			SKUL E 4 Bright A_SkullAttack;
			SKUL F 4 Bright
			SKUL E 4 Bright A_SkullAttack;
			SKUL F 4 Bright  
			SKUL E 4 Bright 
			SKUL F 4 Bright 
			Goto See
	  Death:
		SKUL H 1 Bright A_PlaySound(60);
		SKUL H 8 Bright A_Explode(10, 72);
		EXPP B 4 Bright
		EXPP E 4 Bright
		EXPP F 4 Bright
		Stop
	}
}

actor Cyberdemon: Monster 16
{
	Health 3000
	Radius 40
	Height 110
	Mass 1000
	Speed 160
	PainChance 20
	MeleeRange 338
	+NOSECTORDMG
	States
	{
		Spawn:
			CYBR A 4 A_Look;
			Loop
		See:
			CYBR A 5
			CYBR B 4
			CYBR B 1 A_Chase;
			Loop
		Melee:
			CYBR A 6 A_FaceTarget;
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 1 A_PlaySound(55);
			CYBR F 3 Bright A_FireBullets(0, 0, 1, 9, "BulletPuff");
			CYBR A 6
			Goto See
		Missile:
			CYBR A 4 A_FaceTarget;
			CYBR A 1 A_PlaySound(57);
			CYBR F 3 Bright A_FireProjectile("Rocket");
			CYBR F 3
			CYBR A 4 A_FaceTarget;
			CYBR A 1 A_PlaySound(57);
			CYBR F 3 Bright A_FireProjectile("Rocket");
			CYBR F 3
			CYBR A 4 A_FaceTarget;
			CYBR A 1 A_PlaySound(57);
			CYBR F 3 Bright A_FireProjectile("Rocket");
			CYBR F 3
			CYBR A 5 A_SkullAttack;
			CYBR B 5 A_SkullAttack;
			CYBR A 5 A_SkullAttack;
			CYBR B 5 A_SkullAttack;
			Goto See
		Death:
			CYBR J 8
			CYBR K 6
			CYBR L 6
			CYBR P -1
			Stop
	}
}